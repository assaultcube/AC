persistidents 0

// standard menu definitions

newmenu main

menuitem Multiplayer  [ showmenu multiplayer ]
menuitem Singleplayer [ showmenu singleplayer ]
menuitem Settings     [ showmenu settings ]
menuitem Demo         [ showmenu [Play demo] ]
menuitem Editing      [ showmenu editing ]
menuitem Help         [ showmenu help ]
menuitem About        [ showmenu about ]
menuitem Quit         savequit

// Start maps.
genmapitems = [
  n = (listlen $arg1)
  loop i $n [
    listmapname = (at $arg1 $i)
    menuitemmapload $listmapname
    next = (+ $i 1)
    if (= $next $n) [ next = 0 ] []
    nextmap = (at $arg1 $next)
    (concatword nextmap_ $listmapname) = $nextmap
  ]
]

newmenu maps

// Add maps to the below list to include them in the normal map menu (also for botmaps).
const defaultmaps [ac_aqueous ac_arabian ac_arctic ac_arid ac_avenue ac_cavern ac_complex ac_depot ac_desert ac_desert2 ac_desert3 ac_douze ac_edifice ac_elevation ac_gothic ac_iceroad ac_industrial ac_ingress ac_keller ac_lainio ac_mines ac_outpost ac_power ac_rattrap ac_scaffold ac_shine ac_snow ac_stellar ac_sunset ac_swamp ac_terros ac_toxic ac_urban ac_venison ac_wasteland ac_werk]
genmapitems $defaultmaps
menuitem [] -1
menuitem [Show all maps...] [ showmenu [all maps] ]

newmenu [all maps]
menudirlist "packages/maps" cgz [map $arg1]

newmenu maps_flags
// Add maps to the below list to include them in the CTF, KTF & TKTF map menu. Maps should contain flags here.
const ctfmaps [ac_aqueous ac_arabian ac_arid ac_avenue ac_cavern ac_depot ac_desert3 ac_edifice ac_elevation ac_gothic ac_iceroad ac_industrial ac_ingress ac_keller ac_lainio ac_mines ac_outpost ac_power ac_shine ac_stellar ac_sunset ac_swamp ac_terros ac_urban ac_venison ac_werk]
genmapitems $ctfmaps
menuitem [] -1
menuitem [Show all maps..] [showmenu [all maps]]

newmenu [Play demo]
menuinit [ echo [To stop demo playback, type:] (red)/stopdemo ]
menudirlist demos dmo "demo $arg1"

newmenu rewind

menuitemtextinput "Go to [min]: " [ (if (checkalias tmp_setmr) [return $tmp_setmr]) ] [ tempalias tmp_setmr $arg1 ]
menuitem "\f2Confirm" [ if (checkalias tmp_setmr) [setmr $tmp_setmr; delalias tmp_setmr] ]
menuitem "" -1
menuitemtextinput "Rewind (negative value to forward) [s]: " [ (if (checkalias tmp_rewind) [return $tmp_rewind]) ] [ tempalias tmp_rewind $arg1 ]
menuitem "\f2Confirm" [ if (checkalias tmp_rewind) [rewind $tmp_rewind; delalias tmp_rewind] ]
menuitem "" -1
menuitem "Cancel" [ delalias tmp_setmr; delalias tmp_rewind ]

searchdemo = ""

newmenu [search specific demo]
menudirlist demos dmo "demo $arg1" 0 searchdemo

newmenu [search demo]
menuitemtextinput "Part of demo name or description: " "result $searchdemo" [ searchdemo = $arg1 ]
menuitem "\f2\t\t      Search demo" [ showmenu [search specific demo] ]

// in-game menu that opens when pressing escape while watching a demo.
newmenu [Watching demo]
menuitem "Stop watching demo" stopdemo
menuitem "Restart demo" [ demo $curdemofile ]
menuitem "Rewind demo..." [ showmenu rewind ]
menuitemtextinput "Custom description: " [ getalias (concatword "demodesc_" $curdemofile) ] [ alias (concatword "demodesc_" $curdemofile) $arg1 ] [] 50
menuitem "" -1
menuitem "Watch other demo" [ stopdemo; closecurmenu; showmenu [Play demo] ]
menuitem "Search demo..." [ showmenu [search demo] ]
menuitem "" -1
menuitem "\f4Main menu..." [ closecurmenu; showmenu main ]
menuitem "\f3Quit AssaultCube" savequit

newmenu about

menuitem "AssaultCube Version 1.2" -1
menuitem "" -1
menuitem "AssaultCube is developed by various people around the" -1
menuitem "world. People that enjoy making this game together." -1
menuitem "" -1
menuitem "Thank you to everyone participating to help us make" -1
menuitem "AssaultCube what it is today. To learn about the team" -1
menuitem "(Rabid Viper Productions) behind the game, read here:" -1
menuitem "\f1  http://assault.cubers.net/docs/team.html" -1
menuitem "" -1
menuitem "AssaultCube is a total-conversion of the CUBE engine" -1
menuitem "by Wouter 'Aardappel' van Oortmerssen, and others:" -1
menuitem "\t\f1http://www.cubeengine.com/" -1

newmenu help

menuitem "AssaultCube reference" [ showmenu reference ]
menuitem "Search the reference"  [ showmenu [Reference search] ]
// menuitem "Keyboard controls"     [ showmenu controls ] // This was never implemented or something? - Bukz
menuitem "View the FAQ"          [ load_faq ; showmenu faq ]
menuitem "" -1
menuitem "...for further information/help see the README" [ echo "\f2The readme can be found in the base AssaultCube directory."]

newmenu [Reference search]
menuinit [ docsearchstring = [] ]
menuitemtextinput "Search reference for: " [] [ docsearchstring = $arg1 ]
menuitem "\f2\t  \t [ Search ]\t\t\t" [ docfind $docsearchstring ]

// Show quit dialog if enabled
newmenu quit
menuitem "Yes, quit now" quit
menuitem "No" [ showmenu [] ]
menuitem "" -1
menuitemcheckbox "Don't ask next time: " [(= $askquit 0)] [askquit = (= $arg1 0)]

const savequit [ if $askquit [ showmenu quit ] quit ]

// Create additional menus...
exec config/menus_bot.cfg
exec config/menus_edit.cfg
exec config/menus_settings.cfg
exec config/menus_multiplayer.cfg
exec config/menus_voicecom.cfg
exec config/favourites.cfg

persistidents 1
