persistidents 0

newmenu settings

menuitem [Video settings]           [ showmenu video ]
menuitem [Head-Up Display settings] [ showmenu HUD ]
menuitem [Gameplay settings]        [ showmenu gameplay ]
menuitem [Keyboard settings]        [ showmenu Keyboard ]
menuitem [Mouse settings]           [ showmenu Mouse ]
menuitem [Sound settings]           [ showmenu sound ]
menuitem [Misc settings]            [ showmenu Misc ]
menuitem [Mod package settings]     [ showmenu "Mod package" ]
menuitem [] -1
menuitem [Reset all settings] [ showmenu [Reset settings] ]


newmenu Weapons

menuinit [ menuselection (curmenu) (findlist "assault subgun sniper shotgun carbine" $nextprimary) ]
menumdl weapons/assault/menu mapmodel 75 12
looplist [
  assault "MTP-57 Assault Rifle"
  subgun  "A-ARD/10 Submachine Gun"
  sniper  "Precision Tech AD-81 Sniper Rifle"
  shotgun "V-19 Combat Shotgun"
  carbine "TMP-M&A Carbine"
] [ w desc ] [execute (format [menuitem [%2] [nextprimary %1 ; echo %2 selected] [chmenumdl Weapons weapons/%1/menu mapmodel 75 12] ] $w $desc) ]

newmenu CLA

menuinit [ menuselection CLA (player1 skin_cla) ]
menumdl playermodels/CLA/01 all 50 4
menuitem [Comandante] [ skin_cla 0 ] [ chmenumdl CLA playermodels/CLA/01 all 50 4 ]
menuitem [Psycho]     [ skin_cla 1 ] [ chmenumdl CLA playermodels/CLA/02 all 50 4 ]
menuitem [Bomber]     [ skin_cla 2 ] [ chmenumdl CLA playermodels/CLA/03 all 50 4 ]
menuitem [Ripper]     [ skin_cla 3 ] [ chmenumdl CLA playermodels/CLA/04 all 50 4 ]

newmenu RVSF

menuinit [menuselection RVSF (player1 skin_rvsf)]
menumdl playermodels/RVSF/01 all 50 4
menuitem [Alpha]  [ skin_rvsf 0 ] [ chmenumdl RVSF playermodels/RVSF/01 all 50 4 ]
menuitem [Tango]  [ skin_rvsf 1 ] [ chmenumdl RVSF playermodels/RVSF/02 all 50 4 ]
menuitem [Cobra]  [ skin_rvsf 2 ] [ chmenumdl RVSF playermodels/RVSF/03 all 50 4 ]
menuitem [Viper]  [ skin_rvsf 3 ] [ chmenumdl RVSF playermodels/RVSF/04 all 50 4 ]
menuitem [Swat]   [ skin_rvsf 4 ] [ chmenumdl RVSF playermodels/RVSF/05 all 50 4 ]
menuitem [Police] [ skin_rvsf 5 ] [ chmenumdl RVSF playermodels/RVSF/06 all 50 4 ]

newmenu [QUICK setup]

menuitemtextinput [Nickname:] [] [ name $arg1 ]
menuitemtextinput [Resolution (Width): ] "$scr_w"           [ scr_w $arg1 ]
menuitemtextinput [Resolution (Height): ] "$scr_h"          [ scr_h $arg1 ]
menuitemcheckbox [Fullscreen: ] "$fullscreen"               [ fullscreen $arg1 ]
menuitemtextinput [Mouse sensitivity: ] "$sensitivity"      [ sensitivity $arg1 ]
menuitemcheckbox [Reduced violence mode: ] "$poniesmodeon"  [ reducedviolencemode $arg1 ]
menuitemslider [Graphics detail: ] 1 6 "$gfxqualityalias" 1 [Horrible Low Medium Good High "INSANE! (May perform slowly on older gfx)"] [ new_gfx_quality $arg1 ]
menuitemslider [Sound detail: ] 1 3 "$soundqualityalias" 1 [Low Medium High] [ new_sound_quality $arg1 ]
menuitemslider [Field of view: ] 75 120 "$fov" 2 [] [ fov $arg1 ]
menuitem [] -1
menuitem "\f0\t\t\t        [ OK; View FAQ ]" [ closecurmenu; resetgl; load_faq ; showmenu faq ]
menuitem "\f2\t\t\t        [ OK ]" [ closecurmenu; resetgl ]

const new_gfx_quality [
  if (= $arg1 6) [ // "INSANE!" gfx quality
    fsaa 8
    aniso 8
    minlod 250
    gfxqualityalias = 6
    fpsrange 100 1000
    watersubdiv 1
    dynlight 1
    waterreflect 1
    stencilbits 8
    stencilshadow 40
    dynshadow 40
    lighterror 1
    scorch 1
    bullethole 1
    blood 1
    gib 1
    shotline 1
    echo Be mighty careful! This setting puts any & every graphics setting as high as absolutely possible!
    trilinear 1
    waterrefract 1
    texreduce 0
    hidebigmenuimages 0
  ] [
    if (= $arg1 5) [ // High gfx quality
      fsaa 4
      aniso 4
      minlod 200
      gfxqualityalias = 5
      fpsrange 100 1000
      watersubdiv 2
      dynlight 1
      waterreflect 1
      stencilbits 8
      stencilshadow 40
      dynshadow 40
      lighterror 1
      scorch 1
      bullethole 1
      blood 1
      gib 1
      shotline 1
      trilinear 1
      waterrefract 0
      texreduce 0
      hidebigmenuimages 0
    ] [
      if (= $arg1 4) [ // Good gfx quality
        fsaa 0
        aniso 0
        minlod 100
        gfxqualityalias = 4
        fpsrange 60 60
        watersubdiv 2
        dynlight 1
        waterreflect 1
        stencilbits 8
        stencilshadow 40
        dynshadow 40
        lighterror 4
        scorch 1
        bullethole 1
        blood 1
        gib 1
        shotline 1
        trilinear 1
        waterrefract 0
        texreduce 0
        hidebigmenuimages 0
      ] [
        if (= $arg1 3) [ // Medium gfx quality (DEFAULT)
          fsaa 0
          aniso 0
          minlod 60
          gfxqualityalias = 3
          fpsrange 30 40
          watersubdiv 4
          dynlight 1
          // Makes sure the 3D card is powerful enough for water reflection/shadow stencils...
          if (glext "GL_ARB_fragment_program") [ waterreflect 1 ] [ waterreflect 0 ]
          if (|| (glext "GL_EXT_stencil_two_side") (glext "GL_ATI_separate_stencil")) [
            stencilbits 8
            stencilshadow 40
          ] [
            stencilbits 0
            stencilshadow 0 ]
            dynshadow 40
            lighterror 4
            scorch 1
            bullethole 1
            blood 1
            gib 1
            shotline 1
            trilinear 1
            waterrefract 0
            texreduce 0
            hidebigmenuimages 0
        ] [
          if (= $arg1 2) [ // Low gfx quality
            fsaa 0
            aniso 0
            minlod 60
            gfxqualityalias = 2
            fpsrange 20 25
            watersubdiv 16
            dynlight 0
            waterreflect 0
            stencilshadow 0
            stencilbits 0
            dynshadow 0
            lighterror 10
            scorch 0
            bullethole 0
            blood 0
            gib 0
            shotline 0
            trilinear 1
            waterrefract 0
            texreduce 1
            hidebigmenuimages 1
          ] [ // Horrible gfx quality
            fsaa 0
            aniso 0
            minlod 60
            gfxqualityalias = 1
            fpsrange 20 25
            watersubdiv 64
            dynlight 0
            waterreflect 0
            stencilshadow 0
            stencilbits 0
            dynshadow 0
            lighterror 16
            scorch 0
            bullethole 0
            blood 0
            gib 0
            if (> $gibnum (getvarrange default gibnum)) [gibnum (getvarrange default gibnum)]
            shotline 0
            trilinear 0
            waterrefract 0
            texreduce -1
            hidebigmenuimages 1
          ]
        ]
      ]
    ]
  ]
]

const new_sound_quality [
  if (= $arg1 3) [ // high sound (DEFAULT)
    soundqualityalias = 3
    maxsoundsatonce (getvarrange default maxsoundsatonce)
    footsteps 1
    bulletairsound 1
    bulletbouncesound 1
  ] [
    if (= $arg1 2) [ // medium
      soundqualityalias = 2
      maxsoundsatonce (div (getvarrange default maxsoundsatonce) 2)
      footsteps 1
      bulletairsound 0
      bulletbouncesound 1
    ] [ // low
      soundqualityalias = 1
      maxsoundsatonce (div (getvarrange default maxsoundsatonce) 4)
      footsteps 0
      bulletairsound 0
      bulletbouncesound 0
    ]
  ]
]

newmenu [Reset settings]
menuinit [ resetyes = 0 ] // Ensures this menu works correctly.
menuitemcheckbox [Yes, reset all settings: ] [ resetyes = 1 ] [ resetyes = $arg1 ]
menuitem "\f2         [ Apply! ]        " [ if $resetyes [ resetcfg 1; quit ] [ showmenu [] ] ] [ echo (c 3)If the above option is ticked, AssaultCube will now quit and your settings will be reset. ] // If checkbox is ticked, reset settings and quit, otherwise, close menus.

newmenu Keyboard

menuitem [Select an action and press ENTER to bind a new key:] -1
menuitem [] -1
menuitemkeyinput [Forwards: ]        forward
menuitemkeyinput [Left: ]            left
menuitemkeyinput [Backwards: ]       backward
menuitemkeyinput [Right: ]           right
menuitemkeyinput [Attack: ]          attack
menuitemkeyinput [Special action: ]  altaction
menuitemkeyinput [Jump: ]            jump
menuitemkeyinput [Crouch: ]          crouch
menuitemkeyinput [Reload: ]          reload
menuitemkeyinput [Weapon menu: ]     key_showmenuweapons
menuitemkeyinput [Next weapon: ]     key_universaldelta_plus
menuitemkeyinput [Previous weapon: ] key_universaldelta_minus
menuitemkeyinput [Last weapon: ]     key_prevweapon
menuitemkeyinput [Show scores: ]     key_showscores
menuitemkeyinput [Chat: ]            saycommand
menuitemkeyinput [Team chat: ]       key_teamchat
menuitemkeyinput [Voice menu: ]      key_showmenuvoicecom
menuitemkeyinput [Commandline: ]     key_commandline
menuitemkeyinput [Console output: ]  key_toggleconsole
menuitemkeyinput [Screenshot: ]      screenshot
menuitemkeyinput [Map overview: ]    key_showmap
menuitemkeyinput [Drop flag: ]       dropflag
menuitemkeyinput [Fullscreen: ]      fullscreentoggle
menuitemkeyinput [Edit map: ]        edittoggle
menuitem [] -1
menuitemtextinput [Lookup a keybind: ] [] [ echo []; echo (concatword (c 9) [Contents of keybind ] $arg1 :); echo (keybind $arg1) ] [] 256
menuitem [Alternate edit binds...] [ showmenu [Alternate edit binds] ]
menuitem [] -1
menuitem [Reset keybinds to default settings...] [ showmenu [Reset Binds] ]

newmenu Mouse
menuitemcheckbox [Invert mouse: ] "$invmouse" [ invmouse $arg1 ]
menuitemcheckbox [Automatic Sniper-scope sensitivity: ] "$autoscopesens"  [ autoscopesens $arg1 ]
menuitemtextinput [Mouse Sensitivity:                    ] "$sensitivity" [ sensitivity $arg1 ]
menuitemtextinput [Mouse Acceleration:                    ] "$mouseaccel" [ mouseaccel $arg1 ]
menuitemtextinput [Filter mouse (for bad ones):  ] "$mfilter"             [ mfilter $arg1 ]
menuitemtextinput [Sniper-scope sensitivity scale:  ] "$scopesensscale"   [ scopesensscale $arg1 ]
menuitem [] -1
menuitemslider [Crosshair size: ] 0 50 "$crosshairsize" 5 [] [ crosshairsize $arg1 ]
menuitem [Change crosshair...                ]               [ showmenu Crosshairs ]
menuitem [Change scope crosshair...            ]             [ showmenu [Scope crosshairs] ]
menuitem [Change crosshair in edit mode...     ]             [ showmenu [Edit mode crosshairs] ]

newmenu Crosshairs
menudirlist "packages/crosshairs" png [ loadcrosshair default (concatword $arg1 .png) ] 1
menuinit [sleep 1 menuselectionpersistent]

newmenu [Scope crosshairs]
menudirlist "packages/crosshairs" png [ loadcrosshair scope (concatword $arg1 .png) ] 1
menuinit [sleep 1 menuselectionpersistent]

newmenu [Edit mode crosshairs]
menudirlist "packages/crosshairs" png [ loadcrosshair edit (concatword $arg1 .png) ] 1
menuinit [sleep 1 menuselectionpersistent]

newmenu [Alternate edit binds]
menuitem [Back] [ closecurmenu ]
menuitem [Click to use alternate edit binds] [ exec config/opt/alternateeditbinds.cfg; closecurmenu ] [ echo (red) See config/opt/alternateeditbinds.cfg ]

newmenu [Reset Binds]
menuinit [ resetbindsyes = 0 ] // Ensures this menu works correctly.
menuitemcheckbox [Yes, reset all binds: ] [ resetbindsyes = 1 ] [ resetbindsyes = $arg1 ]
menuitem "\f2         [ Apply! ]        " [ if $resetbindsyes resetbinds [ showmenu [] ] ]

alias defaultgamma 100

newmenu video

menuitemtextinput [Screen width:                 ] "$scr_w" [ scr_w $arg1 ] [] 11
menuitemtextinput [Screen height:                ] "$scr_h" [ scr_h $arg1 ] [] 11
menuitem [Choose resolution from list...] [ showmenu [Display resolutions] ]
menuitem [] -1
menuitemslider [Graphics detail: ] 1 6 "$gfxqualityalias" 1 [Horrible Low Medium Good High "INSANE! (May perform slowly on older gfx)"] [ new_gfx_quality $arg1 ]
menuitem [] -1
menuitemslider [Field of view: ] 75 120 "$fov" 2 []                 [ fov $arg1 ]
menuitemcheckbox [Fullscreen: ] "$fullscreen"                       [ fullscreen $arg1 ]
menuitemslider [Vsync: ] -1 1 "$vsync" 1 [Default Disabled Enabled] [ vsync $arg1 ]
menuitem [] -1
menuitemslider [Set temporary gamma for this map: ] 30 300 "$gamma" 5 [] [ gamma $arg1 ]
menuitemtextinput [Set default gamma for this map: ] [$gamma]            [setgamma $arg1]
menuitemtextinput [Set default gamma for maps without own default: ]     [$defaultgamma] [ defaultgamma = $arg1 ]
menuitem [] -1
menuitem [Advanced video settings...] [ showmenu [Advanced video settings] ]
menuitem [] -1
menuitem "\f2\t\t\t  [ Apply now! ]"   [ resetgl ]

newmenu [Display resolutions]

menuitem [800x600 (4:3)] [ scr_w 800; scr_h 600; closecurmenu; showmenu video ]
menuitem [1024x768 (4:3)] [ scr_w 1024; scr_h 768; closecurmenu; showmenu video ]
menuitem [1280x720 (16:9)] [ scr_w 1280; scr_h 720; closecurmenu; showmenu video ]
menuitem [1280x800 (16:10)] [ scr_w 1280; scr_h 800; closecurmenu; showmenu video ]
menuitem [1280x1024 (5:4)] [ scr_w 1280; scr_h 1024; closecurmenu; showmenu video ]
menuitem [1360x768 (~16:9)] [ scr_w 1360; scr_h 768; closecurmenu; showmenu video ]
menuitem [1366x768 (~16:9)] [ scr_w 1366; scr_h 768; closecurmenu; showmenu video ]
menuitem [1440x900 (16:10)] [ scr_w 1440; scr_h 900; closecurmenu; showmenu video ]
menuitem [1600x900 (16:9)] [ scr_w 1600; scr_h 900; closecurmenu; showmenu video ]
menuitem [1680x1050 (16:10)] [ scr_w 1680; scr_h 1050; closecurmenu; showmenu video ]
menuitem [1920x1080 (16:9)] [ scr_w 1920; scr_h 1080; closecurmenu; showmenu video ]
menuitem [1920x1200 (16:10)] [ scr_w 1920; scr_h 1200; closecurmenu; showmenu video ]

const fsaa_valuemap [-1 0 2 4 8 16]
const aniso_valuemap [0 2 4 8 16]
const minlod_valuemap [60 100 200 250]
const watersubdiv_valuemap [16 8 4 2 1]

newmenu [Advanced video settings]

menuitemslider [Scope FOV: ] 5 60 "$scopefov" 5 [] [ scopefov $arg1 ]
menuitemslider [Antialiasing (FSAA): ] 0 5 "(findlist $fsaa_valuemap $fsaa)" 1 [Default Off 2 4 8 16] [ fsaa (at $fsaa_valuemap $arg1) ]
menuitemslider [Anisotropic filtering: ] 0 4 "(findlist $aniso_valuemap $aniso)" 1 [Off 2 4 8 16] [ aniso (at $aniso_valuemap $arg1) ]
menuitemslider [Minimal level of detail: ] 0 3 "(findlist $minlod_valuemap $minlod)" 1 [Low Decent Good Best] [ minlod (at $minlod_valuemap $arg1) ]
menuitemslider [Light error: ] 1 100 "$lighterror" 5 [] [ lighterror $arg1 ]
menuitemslider [Water subdivision: ] 0 4 "(findlist $watersubdiv_valuemap $watersubdiv)" 1 ["Lowest quality" "Low quality" "Medium quality" "High quality" "Highest quality"] [ watersubdiv (at $watersubdiv_valuemap $arg1) ]
menuitemslider [Dynamic shadows: ] 0 2 getshadowq 1 [Off Blob Stencil] [ setshadowq $arg1 ]
menuitemslider [Tex-reduce: ] -1 3 "$texreduce" 1 []  [ texreduce $arg1 ]
menuitemcheckbox [Water reflection: ] "$waterreflect" [ waterreflect $arg1 ]

const getshadowq [
  if (&& $stencilshadow (>= $stencilbits 8)) [result 2] [
    if $dynshadow [result 1] [result 0]
  ]
]

const setshadowq [
  if (= $arg1 2) [
    stencilshadow 40
    stencilbits 8
    dynshadow 40
  ] [
    if (= $arg1 1) [
      stencilshadow 0
      stencilbits 0
      dynshadow 40
    ] [
      stencilshadow 0
      stencilbits 0
      dynshadow 0
    ]
  ]
]

menuitemcheckbox [Dynamic lights: ] "$dynlight" [ dynlight $arg1 ]
menuitemcheckbox [Bulletholes: ] "$bullethole"  [ bullethole $arg1 ]
menuitemcheckbox [Scorch: ] "$scorch"           [ scorch $arg1 ]
menuitem [Enable 0.93 FOV compatiblity...]      [ fovcompat; sleep 100 fovcompatmenu ]
menuitem [] -1
menuitem [FPS Range...] [ showmenu [FPS Range] ]
menuitem [] -1
menuitem "\f2\t\t        [ Apply now! ]" [ resetgl ]

const fovcompatmenu [ showmenu settings; showmenu video; showmenu [Advanced video settings] ]

newmenu [FPS Range]

menuitem [My 486 0wnz y00       (8-10 FPS)]   [ closecurmenu; fpsrange 8 10 ]
menuitem [Slow machine           (20-25 FPS)] [ closecurmenu; fpsrange 20 25 ]
menuitem [Sensible default       (30-40 FPS)] [ closecurmenu; fpsrange 30 40 ]
menuitem [FPS whore           (60-60 FPS)]    [ closecurmenu; fpsrange 60 60 ]
menuitem [I don't understand LOD (100+ FPS)]  [ closecurmenu; fpsrange 100 1000 ]


const default_bursts [3 2 2]
const refreshcurmenu [ push __curmenu (curmenu); closecurmenu; showmenu (pop __curmenu) ]

newmenu [Weapon Settings]
menuitemslider [Play hit sounds: ] 0 2 "$hitsound" 1 [Off "When server detects hits" "When client detects hits"] [ hitsound $arg1 ]
menuitemcheckbox [Auto weapon reload: ] "$autoreload" [ autoreload $arg1 ]
menuitemcheckbox [Auto switch to akimbo upon pickup: ] "$akimboautoswitch" [ akimboautoswitch $arg1 ]
menuitemslider [Akimbo end action: ] 0 3 "$akimboendaction" 1  ["Switch to knife" "Stay with pistol" "Switch to grenades" "Switch to primary"] [ akimboendaction $arg1 ]
menuitemcheckbox [SMG - Full Auto?] "(= (burstshots SUBGUN -1) 0)" [ if (= $arg1 0) [ burstshots SUBGUN (at $default_bursts 0) ] [ burstshots SUBGUN 0 ]; refreshcurmenu ]
menuitemcheckbox [AR - Full Auto?] "(= (burstshots ASSAULT -1) 0)" [ if (= $arg1 0) [ burstshots ASSAULT (at $default_bursts 1) ] [ burstshots ASSAULT 0 ]; refreshcurmenu ]
menuitemcheckbox [Akimbo - Full Auto?] "(= (burstshots AKIMBO -1) 0)" [ if (= $arg1 0) [ burstshots AKIMBO (at $default_bursts 2) ] [ burstshots AKIMBO 0 ]; refreshcurmenu ]
menuitemslider [SMG - Shots per burst: ] 0 29 "(burstshots SUBGUN -1)" 1 [] [ burstshots SUBGUN $arg1; refreshcurmenu ]
menuitemslider [AR - Shots per burst: ] 0 19 "(burstshots ASSAULT -1)" 1 [] [ burstshots ASSAULT $arg1; refreshcurmenu ]
menuitemslider [Akimbo - Shots per burst: ] 0 19 "(burstshots AKIMBO -1)" 1 [] [ burstshots AKIMBO $arg1; refreshcurmenu ]

alias nickhighlight 0
alias __gibsgore 1

newmenu gameplay
menuitemtextinput [Nickname:        ] "result $curname" [ name $arg1 ]
menuitemslider [Player skins: ] 0 2 "$teamdisplaymode" 1 [Normal "Colored vests" "Full color"] [ teamdisplaymode $arg1 ]
menuitemslider [Custom player skins: ] 0 2 "$hidecustomskins" 1 ["Always show" "Show in non-team modes" "Never show"] [ hidecustomskins $arg1 ]
menuitem [] -1
menuitemcheckbox [Reduced violence mode: ] "$poniesmodeon" [ reducedviolencemode $arg1 ]
menuitemslider [Gib gore: ] 0 4 "$__gibsgore" 1 [Off Normal Good Messy Unrealistic] [ gibsgore $arg1 ]
menuitemslider [Name highlight color: ] 0 9 "$nickhighlight" 1 [ "\f0green \f5(default)" "\f1blue" "\f2yellow" "\f3red" "\f4gray" "white" "\f6dark brown" "\f7dark red" "\f8purple" "\f9orange" ] [ nickhighlight = $arg1; HIGHLIGHT = (c $nickhighlight) ]
menuitemcheckbox [Show scores on death: ] "$showscoresondeath" [ showscoresondeath $arg1 ]
menuitem [Change to the enemy team]                            changeteam
menuitem [Change weapon...]                                    [ showmenu Weapons ]
menuitem [Change player skin...]                               [ showmenu (at [CLA RVSF] (player1 team)) ]
menuitem [Weapon settings...]                                  [ showmenu [Weapon Settings] ]
menuitem [Edit kill messages...]                               [ showmenu [Kill Messages] ]
menuitem [Bot settings...]                                     [ showmenu [Bot settings] ]

tempalias __screenshottype 0
alias askquit 1
alias showmenuonstartup 1
alias menufilesort_cgz 0
alias menufilesort_dmo 0

newmenu Misc
menuitemcheckbox [Auto map download: ] "$autogetmap"                 [ autogetmap $arg1 ]
menuitem         [Configure auto media download --> ]                [ showmenu "Autodownload config"]
menuitemcheckbox [Show gamemodes as acronyms: ] "$modeacronyms"      [ modeacronyms $arg1 ]
menuitemcheckbox [Show quit dialog when quitting: ] "$askquit"       [ askquit = $arg1 ]
menuitemcheckbox [Show main menu when starting AC: ] "$showmenuonstartup"   [ showmenuonstartup = $arg1 ]
menuitemcheckbox [Auto-screenshot: ] "$autoscreenshot"               [ autoscreenshot $arg1 ]
menuitemcheckbox [Allow HUD echos: ] "$allowhudechos"                [ allowhudechos $arg1 ]
menuitemslider [Servers to ping at once: ] 0 50 "$maxservpings" 5 [] [ maxservpings $arg1 ]
menuitemslider [Milliseconds between pinging servers: ] 1000 60000 "$servpingrate" 1000 [] [ servpingrate $arg1 ]
menuitemslider [Screenshot type: ] -1 4 "$__screenshottype" 1 ["JPEG (Terrible Quality)" "JPEG (Bad Quality)" "JPEG (Good Quality)" "JPEG (Best Quality)" "PNG (Lossless)" "BMP (Lossless)"] [ if (= $arg1 -1) [ screenshottype 1; jpegquality 50; __screenshottype = -1 ] [ if (= $arg1 0) [ screenshottype 1; jpegquality 70; __screenshottype = 0 ] [ if (= $arg1 1) [ screenshottype 1; jpegquality 85; __screenshottype = 1 ] [ if (= $arg1 2) [ screenshottype 1; jpegquality 100; __screenshottype = 2 ] [ if (= $arg1 3) [ screenshottype 2; __screenshottype = 3 ] [ if (= $arg1 4) [ screenshottype 0; __screenshottype = 4 ] ] ] ] ] ] ]
menuitemslider [Console size: ] 0 20 "$consize" 1 [] [ consize $arg1 ]
menuitemslider [Font type: ] 0 2 "$__fontsetting" 1 [Default Serif Monospace] [ if (= $arg1 1) [ setfont serif ] [ if (= $arg1 2) [ setfont mono ] [ setfont default ] ] ]
menuitemslider [Mini-map background style: ] 0 2 "$showmapbackdrop" 1 ["Fully Transparent" Black White/Transparent] [ showmapbackdrop $arg1 ]
menuitemcheckbox [Enable compatibility with older cubescripts: ] "$compatibilitymode" [ compatibilitymode $arg1; echo (c 3) This setting requires a restart to take effect. ]
menuitemslider [Menu map file sort order: ] 0 (- (listlen (listoptions menufilesortorders)) 1) "$menufilesort_cgz" 1 (listoptions menufilesortorders) [menufilesort_cgz = $arg1]
menuitemslider [Menu demo file sort order: ] 0 (- (listlen (listoptions menufilesortorders)) 1) "$menufilesort_dmo" 1 (listoptions menufilesortorders) [menufilesort_dmo = $arg1]

// Reduced game violence mode...
alias poniesmodeon 0

const reducedviolencemode [
  push x (! $arg1)
  poniesmodeon = (! $x)
  blood $x
  gib $x
  looplist "26 27 28 29 30 31 32 33 48" i [ mutesound $i $x ] // (Un-) Mute pain sounds: 26 to 33 and 48
  echo "\f2OMG! \f8PW\f1NI\f3ES\f5! mode" (if $x [result "has been disabled."] [result "enabled - Violent game content has now been reduced."])
  pop x
]

const DEFAULT_SCOREBOARD_NAMES [Flags Frags Deaths Ratio Score Lag "Client number" Name]
const DEFAULT_SCOREBOARD_ORDER [sc_flags sc_frags sc_deaths sc_ratio sc_score sc_lag sc_clientnum sc_name]
const DEFAULT_SCOREBOARD_POSITIONS [0 1 2 -1 4 5 6 7]
const SCOREBOARD_DISABLE_ITEMS [sc_deaths sc_ratio sc_score sc_lag]

const refreshcurmenudelayed [ push __curmenu (curmenu); closecurmenu; sleep 0 [ showmenu (pop __curmenu) ] ]

newmenu [Customize your scoreboard]
menuitem [0 has the highest priority...] -1
menuitem [-1 disables the item entirely...] -1
menuitem [] -1
loop tsl (listlen $DEFAULT_SCOREBOARD_ORDER) [
  ccn = (at $DEFAULT_SCOREBOARD_ORDER $tsl)
  cfn = (at $DEFAULT_SCOREBOARD_NAMES (findlist $DEFAULT_SCOREBOARD_ORDER $ccn))
  if (!= (findlist $SCOREBOARD_DISABLE_ITEMS $ccn) -1) [
    execute (format [menuitemslider [%1: ] -1 7 "(getalias %2)" 1 [] [ %2 $arg1 ]] $cfn $ccn)
  ] [
    execute (format [menuitemslider [%1: ] 0 7 "(getalias %2)" 1 [] [ %2 $arg1 ]] $cfn $ccn)
  ]
]
menuitem [] -1
menuitem [Reset to default scoreboard...] [ loop rsl (listlen $DEFAULT_SCOREBOARD_ORDER) [ (at $DEFAULT_SCOREBOARD_ORDER $rsl) (at $DEFAULT_SCOREBOARD_POSITIONS $rsl) ]; refreshcurmenudelayed ]
menuitem [Close] [ closecurmenu ]

alias wallclockformat "" // strftime formatting
alias wallclockformatlist ["" %H:%M %H:%M:%S %I:%M%p %I:%M:%S%p U%H:%M U%H:%M:%S] //"U" at beginning means UTC/GMT time

newmenu HUD

menuitem [Show:] -1
menuitemcheckbox [   Gun ] "$hudgun"                                           [ hudgun $arg1 ]
menuitemcheckbox [   Team icon ] "(= $hideteam 0)"                             [ hideteam (= $arg1 0) ]
menuitemcheckbox [   Icons ] "(= $hidehudequipment 0)"                         [ hidehudequipment (= $arg1 0) ]
menuitemcheckbox [   Console ] "(= $hideconsole 0)"                            [ hideconsole (= $arg1 0) ]
menuitemcheckbox [   Player names ] "$showtargetname"                          [ showtargetname $arg1 ]
menuitemcheckbox [   Spectator info ] "(= $hidespecthud 0)"                    [ hidespecthud (= $arg1 0) ]
menuitemcheckbox [   Quick messages ] "(= $hidehudmsgs 0)"                     [ hidehudmsgs (= $arg1 0) ]
menuitemcheckbox [   Damage direction indicator ] "(= $hidedamageindicator 0)" [ hidedamageindicator (= $arg1 0) ]
menuitemcheckbox [   Damage screen flash    ] "$damagescreen"                  [ damagescreen $arg1 ]
menuitemcheckbox [   Crosshair team-sign ] "$crosshairteamsign"                [ crosshairteamsign $arg1 ]
menuitemcheckbox [   CTF icons ]    "(= $hidectfhud 0)"                        [ hidectfhud (= $arg1 0) ]
menuitemcheckbox [   Radar ] "(= $hideradar 0)"                                [ hideradar (= $arg1 0) ]
menuitemslider   [   Radar zoom: ] 5 500                                       [ $radarheight ] 5 [][ radarheight $arg1 ]
menuitemslider   [   Votes: ] 0 2 "$hidevote" 1 ["Always show" "Show until you vote" "Never show"] [ hidevote $arg1 ]
menuitemslider   [   Graphics statistics: ] 0 2 "$showstats" 1 [None "FPS Only" All] [ showstats $arg1 ]
menuitemslider   [   Game time: ] 0 2 "$gametimedisplay" 1 [None "Count backward" "Count forward"] [ gametimedisplay $arg1 ]
menuitemslider   [   Wall clock: ] 0 6 [findlist $wallclockformatlist $wallclockformat] 1 [None "HH:MM (local, 24h)" "HH:MM:SS (local, 24h)" "HH:MM (local, 12h)" "HH:MM:SS (local, 12h)" "HH:MM (UTC/GMT)" "HH:MM:SS (UTC/GMT)"] [ persistidents 1; alias wallclockformat (at $wallclockformatlist $arg1); persistidents 0 ]
menuitemslider   [   Extra messages: ] 0 3 "$hudextras" 1 ["No extra messages" "Show extra messages in console" "Pop up extra messages!" "SPAM EXTRA MESSAGES!"] [ hudextras $arg1 ]
menuitemslider   [   Hand alignment: ] 0 1 "$righthanded" 1 [Left-handed Right-handed] [ righthanded $arg1 ]
menuitemslider   [   Max-roll: ] 0 20 "$maxroll" 1 [] [ maxroll $arg1 ]
menuitemslider   [   Spectator max-roll: ] 0 1 "$maxrollremote" 1 [Off On] [ maxrollremote $arg1 ]
menuitem [] -1
menuitem [Customize your scoreboard...] [ showmenu [Customize your scoreboard] ]

newmenu sound

menuitemslider [Sound volume: ] 0 255 "$soundvol" 16 [] [ soundvol $arg1 ]
menuitemslider [Music volume: ] 0 255 "$musicvol" 16 [] [ musicvol $arg1 ]
menuitemslider [Sound detail: ] 1 3 "($soundqualityalias)" 1 [Low Medium High] [ new_sound_quality $arg1 ]
menuitem [] -1
menuitem [Mute/Un-mute specific sounds...] [ showmenu [Mute specific sounds] ]
menuitem [] -1
menuitem [Advanced settings:] -1
menuitemslider [   Sounds at once: ] 0 100 "$maxsoundsatonce" 2 []            [ maxsoundsatonce $arg1 ]
menuitemslider [   Voice-coms: ] 0 2 "$voicecomsounds" 1 [Off On "Team only"] [ voicecomsounds $arg1 ]
menuitemslider [   Heartbeat: ] 0 99 "$heartbeat" 5 []                        [ heartbeat $arg1 ]
menuitemcheckbox [   Footsteps: ] "$footsteps"                                [ footsteps $arg1 ]
menuitemcheckbox [   Disable own footsteps: ] "(= $localfootsteps)"           [ localfootsteps (! $arg1) ]
menuitemcheckbox [   Bullet hit: ] "$bulletbouncesound"                       [ bulletbouncesound $arg1 ]
menuitemcheckbox [   Bullet near-miss: ] "$bulletairsound"                    [ bulletairsound $arg1 ]

const muteitem [ menuitemcheckbox (concatword $arg1 [: ]) (concat ! "(soundmuted" $arg2 ")") (concat mutesound $arg2 "$arg1") ]

newmenu [Mute specific sounds]

menuitem [Weapon sounds]           [ showmenu [Mute weapon sounds] ]
menuitem [Bullet/explosion sounds] [ showmenu [Mute bullet/explosion sounds] ]
menuitem [Pain/death sounds]       [ showmenu [Mute pain/death sounds] ]
menuitem [Pickup sounds]           [ showmenu [Mute pickup sounds] ]
menuitem [Movement sounds]         [ showmenu [Mute movement sounds] ]
menuitem [Voice-com sounds]        [ showmenu [Mute voice-coms] ]
menuitem [Other sounds]            [ showmenu [Mute other sounds] ]
menuitem [] -1
menuitem [Un-mute all sounds...] [ unmuteallsounds ]

newmenu  [Mute weapon sounds]

muteitem [Knife] 7
muteitem [Pistol] 8
muteitem [Pistol reloading] 9
muteitem [Carbine] 10
muteitem [Carbine reloading] 11
muteitem [Shotgun] 12
muteitem [Shotgun reloading] 13
muteitem [Submachine gun] 14
muteitem [Submachine gun reloading] 15
muteitem [Sniper] 16
muteitem [Sniper reloading] 17
muteitem [Assault Rifle] 18
muteitem [Assault Rifle reloading] 19
muteitem [Grenade pull] 41
muteitem [Grenade throw] 42
muteitem [Grenade bounce 1] 43
muteitem [Grenade bounce 2] 44
muteitem [Akimbo reload] 45
muteitem [Change weapon] 46
muteitem [Empty clip] 24

newmenu [Mute pickup sounds]

muteitem [Ammo pickup] 20
muteitem [Health pickup] 21
muteitem [Armour pickup] 22
muteitem [Akimbo pickup] 23
muteitem [Akimbo finished] 25
muteitem [Helmet pickup] 99

newmenu [Mute pain/death sounds]

muteitem [Pain 1] 26
muteitem [Pain 2] 27
muteitem [Pain 3] 28
muteitem [Pain 4] 29
muteitem [Pain 5] 30
muteitem [Pain 6] 31
muteitem [Die 1] 32
muteitem [Die 2] 33
muteitem [Gib sounds] 48
muteitem [Tinnitus] 62
muteitem [Heartbeat] 100

newmenu [Mute movement sounds]

muteitem [Footsteps] 53
muteitem [Crouch] 54
muteitem [Water footsteps] 55
muteitem [Water crouching] 56
muteitem [Crouch-in] 57
muteitem [Crouch-out] 58
muteitem [Jump] 0
muteitem [Soft landing] 1
muteitem [Hard landing] 2
muteitem [Splash 1] 35
muteitem [Splash 2] 36
muteitem [Underwater] 61

newmenu [Mute bullet/explosion sounds]

muteitem [Ricochet air 1] 3
muteitem [Ricochet air 2] 4
muteitem [Ricochet hit] 5
muteitem [Bullet (water impact)] 6
muteitem [Grenade explosion] 34
muteitem [Hit sound] 47

newmenu [Mute other sounds]

muteitem [Flag drop] 37
muteitem [Flag pickup] 38
muteitem [Flag return] 39
muteitem [Flag score] 40
muteitem [Headshot] 49
muteitem [Call vote] 50
muteitem [Pass vote] 51
muteitem [Fail vote] 52
muteitem [Menu select] 59
muteitem [Menu enter] 60

newmenu [Mute voice-coms]

muteitem [Affirmative] 63
muteitem [All-right sir] 64
muteitem [Come on, move] 65
muteitem [Coming in with the flag] 66
muteitem [Cover me] 67
muteitem [Defend the flag] 68
muteitem [Enemy down] 69
muteitem [Go get 'em boys!] 70
muteitem [Good job team] 71
muteitem [I got one!] 72
muteitem [I made contact] 73
muteitem [I'm attacking] 74
muteitem [I'm on defense] 75
muteitem [I'm on your team] 76
muteitem [Negative] 77
muteitem [No can do] 78
muteitem [Recover the flag] 79
muteitem [Sorry] 80
muteitem [Spread out] 81
muteitem [Stay here] 82
muteitem [Stay together] 83
muteitem [There's no way sir] 84
muteitem [We did it!] 85
muteitem [Yes] 86
muteitem [Under way] 87
muteitem [On the move] 88
muteitem [Got your back] 89
muteitem [Got you covered] 90
muteitem [In position] 91
muteitem [In position now] 92
muteitem [Report In!] 93
muteitem [Nice shot] 94
muteitem [Thanks] 95
muteitem [Thanks, man] 96
muteitem [Awesome (1)] 97
muteitem [Awesome (2)] 98

newmenu [Kill Messages]
menuitemvar [concat (orange)Frag messages: ] [] []
menuitemtextinput [Pistol: ]  [fragmessage 1] [ fragmessage 1 $arg1 ] [] 15
menuitemtextinput [Carbine: ] [fragmessage 2] [ fragmessage 2 $arg1 ] [] 15
menuitemtextinput [Shotgun: ] [fragmessage 3] [ fragmessage 3 $arg1 ] [] 15
menuitemtextinput [SMG: ]     [fragmessage 4] [ fragmessage 4 $arg1 ] [] 15
menuitemtextinput [Sniper: ]  [fragmessage 5] [ fragmessage 5 $arg1 ] [] 15
menuitemtextinput [AR: ]      [fragmessage 6] [ fragmessage 6 $arg1 ] [] 15
menuitemtextinput [CPistol: ] [fragmessage 7] [ fragmessage 7 $arg1 ] [] 15
menuitemtextinput [Akimbo: ]  [fragmessage 9] [ fragmessage 9 $arg1 ] [] 15
menuitem [] -1
menuitemvar [concat (orange)Gib messages: ] [] []
menuitemtextinput [Knife: ]    [gibmessage 0] [ gibmessage 0 $arg1 ] [] 15
menuitemtextinput [Shotgun: ]  [gibmessage 3] [ gibmessage 3 $arg1 ] [] 15
menuitemtextinput [Sniper: ]   [gibmessage 5] [ gibmessage 5 $arg1 ] [] 15
menuitemtextinput [Grenades: ] [gibmessage 8] [ gibmessage 8 $arg1 ] [] 15

tempalias __pckserv ""

newmenu "Autodownload config"
menuinit [
    delmenu (curmenu) ; newmenu (curmenu)
    push serverlist (getpckserver)
    push words (listlen $serverlist)
    if (! $words) [
        menuitem "no package servers configured" -1
    ] [
        if (= $words 1) [
            menuitem $serverlist -1
        ] [
            menuitem "\f2priority\tping\tstatus\thost address" -1
            looplist $serverlist [host priority ping resolved] [
                resolved = (at ["\f2no response" "\f0ok"] $resolved)
                if (<= $priority -1000) [ resolved = "\f2disabled" ]
                if (<= $priority -10000) [ resolved = "\f3deleted" ]
                menuitem (concatword (if (<= $priority -1000) "" $priority) "\t" $ping "\t\fs" $resolved "\fr\t" $host) (concat alias __pcksrv "[" (escape $host) $priority $ping (escape $resolved) "] ; showmenu  [Configure package server]")
            ]
        ]
    ]
    menuitem "" -1
    menuitemcheckbox [Enable auto media download: ] "$autodownload" [ autodownload $arg1 ]
    menuitem "Add package server -->" [ inputcommand "" "addpckserver $cmdbuf" "enter host address:" ]
    menuitem "" -1
    menuitem "\f4you need to restart the game for changes to take effect" -1
    pop serverlist words
]

newmenu "Configure package server"
menuinit [
    delmenu (curmenu) ; newmenu (curmenu)
    if (!= (listlen $__pcksrv) 4) [
        menuitem "----- no server selected ------" -1
    ] [
        push priority (at $__pcksrv 1)
        menuitem (concatword "Host address \t\f2" (at $__pcksrv 0)) -1
        menuitemtextinput "Priority     \t" $priority [addpckserver (at $__pcksrv 0) $arg1]
        menuitem (concatword "Ping         \t" (at $__pcksrv 2)) -1
        menuitem (concatword "Status       \t" (at $__pcksrv 3)) -1
        menuitem "" -1
        if (<= $priority -1000) [
            menuitem "\f0[Enable server]" (concat addpckserver (at $__pcksrv 0) 0 "; closecurmenu")
        ] [
            menuitem "\f3[Disable server]" (concat addpckserver (at $__pcksrv 0) -1000 "; closecurmenu")
        ]
        if (> $priority -10000) [
            menuitem "\f3[Mark server for deletion]" (concat addpckserver (at $__pcksrv 0) -10000 "; closecurmenu")
        ]
        pop priority
    ]
]

tempalias __zip ""

newmenu "Mod package"
menuinit [
    delmenu (curmenu) ; newmenu (curmenu)
    menurenderoffset -20 0
    push ecurmenu (escape (curmenu))
    push activezips (zipmodlist active)
    push inactivezips (zipmodlist inactive)
    if (strlen $activezips) [
        menuitem "\f2Currently used mod packages (highest priority listed first)" -1 (concat chmenutexture $ecurmenu "")
        looplist $activezips zip [
            menuitem (concatword "\fs\f9" $zip ":\fr " (at (zipmodgetdesc $zip) 0)) (concat alias __zip $zip "; showmenu  [Configure mod package]") (concatword chmenutexture " " $ecurmenu "../modpreviews/" $zip ".jpg " $zip)
        ]
        menuitem "" -1 (concat chmenutexture $ecurmenu "")
    ]
    if (strlen $inactivezips) [
        menuitem "\f2Unused mod packages" -1 (concat chmenutexture $ecurmenu "")
        looplist $inactivezips zip [
            menuitem (concatword "\fs\f9" $zip ":\fr " (at (zipmodgetdesc $zip) 0)) (concat alias __zip $zip "; showmenu  [Configure mod package]") (concatword chmenutexture " " $ecurmenu "../modpreviews/" $zip ".jpg " $zip)
        ]
        menuitem "" -1 (concat chmenutexture $ecurmenu "")
    ]
    if (|| (strlen $activezips) (strlen $inactivezips)) [ ] [ menuitem "--- no mod packages available ---" -1 ]
    if (strlen $activezips) [ menuitem "\f3Deactivate all mod packages" zipmodclear ]
    menuitem "\f3Refresh textures (has no effect on already loaded models)" resetgl
    pop activezips inactivezips ecurmenu
]

newmenu "Configure mod package"
menuinit [
    delmenu (curmenu) ; newmenu (curmenu)
    menurenderoffset -20 0
    push ecurmenu (escape (curmenu))
    push activezips (zipmodlist active)
    if (strlen $__zip) [
        chmenutexture (curmenu) (concatword "../modpreviews/" $__zip ".jpg") $__zip
        push __c "\f2"
        looplist (zipmodgetdesc $__zip) desc [ if (strlen $desc) [ menuitem (concat $__c $desc) -1 ] ; alias __c "" ]
        pop __c
        menuitem (concat "\f4revision:" (zipmodgetrevision $__zip)) -1
        menuitem "" -1
        if (< (findlist $activezips $__zip) 0) [
            menuitem (concatword "\f0Add \f9" $__zip "\f0 to the list of used mod packages") (concat addzipmod $__zip)
        ] [
            menuitem (concatword "\f3Remove \f9" $__zip "\f0 from the list of used mod packages") (concat zipmodremove $__zip)
        ]
        menuitem "" -1
        menuitem "Show list of files in package" "showmenu [Show files in package]"
    ] [
        menuitem "no zip selected" -1
    ]
    pop activezips ecurmenu
]

newmenu "Show files in package"
menuinit [
    delmenu (curmenu) ; newmenu (curmenu)
    if (strlen $__zip) [
        menuitem (concat "Files in package\f9" $__zip) -1
        menuitem "" -1
        looplist (zipmodgetfiles $__zip) aa [ menuitem $aa -1 ]
    ] [
        menuitem "no zip selected" -1
    ]
]


persistidents 1
